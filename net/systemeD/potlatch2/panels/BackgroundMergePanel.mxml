<?xml version="1.0" encoding="utf-8"?>
<!---
    The Background Merge Panel allows the tags from two features - one from
    a read-only vector background layer, one from an editable layer - to be
    compared. Tags are highlighted depending whether they match, and tags from
    the background layer can be easily merged.
-->

<mx:VBox
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    xmlns:s="library://ns.adobe.com/flex/spark"
    height="100%">
  <s:RichText id="backgroundMergePanelText" width="100%">Review and Merge tags from the Background layer</s:RichText>
  <mx:DataGrid editable="false" id="backgroundPanelDG" width="100%" height="50%">
    <mx:columns>
      <mx:DataGridColumn editable="false" dataField="k" headerText="Key" />
      <mx:DataGridColumn editable="false" dataField="e" headerText="OSM value" />
      <mx:DataGridColumn editable="false" headerText="Merge" width="150" textAlign="center">
        <mx:itemRenderer>
          <fx:Component>
            <mx:HBox horizontalAlign="center" verticalAlign="middle" width="100%">
              <s:Button width="50" label="&lt;&lt;" visible="{parentDocument.buttonVisible(data.b, data.e)}" click="parentDocument.mergeForKey(data.k);" />
            </mx:HBox>
          </fx:Component>
        </mx:itemRenderer>
      </mx:DataGridColumn>
      <mx:DataGridColumn editable="false" itemRenderer="net.systemeD.potlatch2.panels.BackgroundMergeFieldComponent" dataField="b" headerText="Background value" />
    </mx:columns>
  </mx:DataGrid>
  <mx:ViewStack id="statusStack" resizeToContent="true" width="100%">
    <mx:VBox id="empty" />
    <mx:VBox id="not_complete">
      <s:VGroup width="100%">
        <s:RichText width="100%">If all the information from this feature is accounted for in the main layer, you can mark this feature as 'complete'.</s:RichText>
        <s:RichText width="100%">This lets other contributors see what still needs reconciling.</s:RichText>
        <s:Button label="Mark feature as Complete" click="markComplete()"/>
      </s:VGroup>
    </mx:VBox>
    <mx:VBox id="complete" width="100%">
      <s:VGroup width="100%">
        <s:RichText width="100%">This feature has been marked as 'complete'. If this is incorrect, and there is still reconciling required, you can mark this feature as 'not complete'.</s:RichText>
        <s:Button label="Mark feature as Not complete" click="markNotComplete()"/>
      </s:VGroup>
    </mx:VBox>
  </mx:ViewStack>
  <fx:Script><![CDATA[

      import net.systemeD.halcyon.connection.*;
      import net.systemeD.halcyon.MapPaint;
      import net.systemeD.potlatch2.utils.SnapshotConnection;

      import mx.collections.*;

      private var editableEntity:Entity;
      private var backgroundEntity:Entity;
      private var tagDataProvider:ArrayCollection;

      /**
      * @param entities The two entities that are being compared. One should be from a background layer, but the
      *                 order is not important.
      */
      public function init(entities:Array):void {
          if ( tagDataProvider == null ) {
              tagDataProvider = new ArrayCollection();
              backgroundPanelDG.dataProvider = tagDataProvider;
          }

          if (parentDocument.controller.map.getLayerForEntity(entities[0]).isBackground) {
              backgroundEntity = entities[0];
              editableEntity = entities[1];
          } else {
              backgroundEntity = entities[1];
              editableEntity = entities[0];
          }
          backgroundEntity.addEventListener(Connection.STATUS_CHANGED, statusEvent, false, 0, true);
          setStatusStack();
          editableEntity.addEventListener(Connection.TAG_CHANGED, tagChanged, false, 0, true);
          updateTagDataProvider();
      }

      private function updateTagDataProvider():void {
          var tag:Tag;
          var keys:Array = [];

          tagDataProvider.removeAll();

          for each (tag in backgroundEntity.getTagArray() ) {
              keys.push(tag.key);
          }

          for each (tag in editableEntity.getTagArray() ) {
              keys.push(tag.key);
          }

          keys=keys.filter(function(k:*, i:int, arr:Array):Boolean { return arr.indexOf(k) == i } ); // remove duplicates
          keys.sort();

          for each (var key:String in keys) {
              tagDataProvider.addItem({k:key, e:editableEntity.getTag(key), b:backgroundEntity.getTag(key)});
          }
          backgroundPanelDG.invalidateList();
      }

      private function tagChanged(e:Event):void {
          updateTagDataProvider();
      }

      /**
      * Should the button for merging tags be shown?
      *
      * @param b The tag value for the background entity
      * @param e The tag value for the editable entity
      */
      public function buttonVisible(b:String, e:String):Boolean {
          if (b != null && b != e) {
              return true;
          }
          return false;
      }

      /**
      * Merge the data for the given key. Results in the value being copied to the editable entity, using the
      * global undo stack.
      *
      * @param key The tag key to be merged.
      */
      public function mergeForKey(key:String):void {
          editableEntity.setTag(key, backgroundEntity.getTag(key), MainUndoStack.getGlobalStack().addAction);
      }

      /**
      * Figure out an appropriate background color for the row. Don't call this for things you don't want coloured in. Like for empty tags.
      *
      * @param i The row index in the tagDataProvider
      * @return The colour to be used as the background of the field
      */
      public function getColorFor(i:int):int {
          if (tagDataProvider[i].e == tagDataProvider[i].b) {
              return 0xDDFFDD; // matching, green
           } else if (tagDataProvider[i].e == null) {
              return 0xDDDDFF; // new value, blue
           } else if (tagDataProvider[i].b) {
              return 0xFFDDDD; // conflicting, red
           }
           return NaN; // which is interpreted as black
      }

      private function statusEvent(e:Event):void {
            setStatusStack();
      }

      private function setStatusStack():void {
          switch (backgroundEntity.status) {
              case 'incomplete':
                  statusStack.selectedChild = not_complete;
                  break;
              case 'complete':
                  statusStack.selectedChild = complete;
                  break;
              default:
                  statusStack.selectedChild = empty;
          }
      }

      private function markComplete():void {
          if (backgroundEntity.connection is SnapshotConnection) {
              SnapshotConnection(backgroundEntity.connection).markComplete(backgroundEntity);
          }
      }

      private function markNotComplete():void {
          if (backgroundEntity.connection is SnapshotConnection) {
              SnapshotConnection(backgroundEntity.connection).markNotComplete(backgroundEntity);
          }
      }
  ]]>
  </fx:Script>
</mx:VBox>